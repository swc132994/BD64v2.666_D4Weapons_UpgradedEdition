ACTOR D4BFG : BrutalD464Weapon Replaces BFG9000Spawner
{
Weapon.SelectionOrder 2800
Weapon.AmmoUse 40
Weapon.AmmoGive 40
Weapon.AmmoType "Cell"
Weapon.BobStyle InverseSmooth
Weapon.BobRangeX 0.5
Weapon.BobRangeY 0.3
Weapon.BobSpeed 1.7
Inventory.PickupSound "D4BFG/Pickup"
Inventory.PickupMessage "$GOTBFG9000"
Obituary "$OB_MPBFG_MBF"
Tag "$TAG_BFG9000"
+INVENTORY.RESTRICTABSOLUTELY
+inventory.ignoreskill
+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		D4W7 A -1
		Stop
	KickingFlash:
		BF4S CBA 1
		BF4S A 10
		BF4S ABC 1
		Stop
	AirKickingFlash:
		BF4S CBA 1
		BF4S A 12
		BF4S ABC 1
		Stop
	SlideKickingStart:
		BF4S CBA 1
		BF4S A 19
	SlideKickingEnd:
		BF4S A 7
		BF4S ABC 1
		Stop
	v1Ready:
	Ready:
		BF4G A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"ChangeAltMode")
		Loop
	ChangeAltMode:
		BF4G A 1
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 A_JumpIfInventory("BFGUpgrade",1,1)
		Goto NoUpgrades
		BF4G A 1
		TNT1 A 0 A_JumpIfInventory("BFGUpgrade",2,1)
		Goto NoUpgrades2
		BF4G A 1
		TNT1 A 0 A_JumpIfInventory("D4AltMode",1,"ChangeAltMode2")
		TNT1 A 0 A_GiveInventory("D4AltMode",1)
		TNT1 A 0 A_PlaySound("D4BFG/ModeSwitch",CHAN_WEAPON)
		TNT1 A 0 A_Print("Merciless Extermination Selected",2)
		BF4F ABDEFGHIJKLMNMLKJIHGFEDBA 1 BRIGHT
		Goto Ready
	ChangeAltMode2:
		BF4G A 1
		TNT1 A 0 A_TakeInventory("D4AltMode",1)
		TNT1 A 0 A_PlaySound("D4BFG/ModeSwitch",CHAN_WEAPON)
		TNT1 A 0 A_Print("Purge Bolt Selected",2)
		BF4F ABDEFGHIJKLMNMLKJIHGFEDBA 1 BRIGHT
		Goto Ready
	Deselect:
		BF4S CBA 1
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4BFGSelected",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4BFGSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		BF4S ABC 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_PlaySound("D4BFG/Charge",0)
		BF4C A 0 BRIGHT A_PlaySound("D4BFG/ChargeLoop",6,1,1)
		BF4F ABCDEFGHIJKLM 1 BRIGHT
		BF4C ABCDABCDABCDABCDABCDABCD 1 BRIGHT
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		TNT1 A 0 A_PlaySound("D4BFG/Fire",1)
		TNT1 A 0 A_FireCustomMissile("64BFGBallDeathSound",0,1,6,-3,0,0)
		BF4F M 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		BF4F NOPQ 1 BRIGHT A_SetAngle(random(1,-1) + angle)
		BF4G A 6
		TNT1 A 0 A_Refire
		BF4G A 6 A_WeaponReady(1)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("BFGUpgrade",1,1)
		Goto NoUpgrades
		TNT1 A 0 A_JumpIfInventory("D4AltMode",1,"AltFire2")
		TNT1 A 0 A_JumpIfInventory("Cell",10,1)
		Goto NoAmmo
		BF4F A 0 BRIGHT A_PlaySound("D4BFG/MiniCharge",7)
		BF4F ABCDEFGHIJKLM 1 BRIGHT
		BF4C ABCD 1 BRIGHT
	AltHold:
		TNT1 A 0 A_JumpIfInventory("Cell",10,"ActualAltHold")
		Goto NoAmmo
	ActualAltHold:
		TNT1 A 0 A_TakeInventory("Cell",10)
		TNT1 A 0 A_PlaySound("D4BFG/MiniFire",1)
		TNT1 A 0 A_FireCustomMissile("PurgeBolt",0,1,6,-3)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		BF4F M 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		BF4F NOPQ 1 BRIGHT A_SetAngle(random(1,-1) + angle)
		BF4G A 6
		TNT1 A 0 A_Refire
		BF4G A 6 A_WeaponReady(1)
		Goto Ready
	AltFire2:
		TNT1 A 0 A_JumpIfInventory("Cell",100,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("D4BFG/Charge2",0)
		BF4C A 0 BRIGHT A_PlaySound("D4BFG/ChargeLoop2",6,1,1)
		BF4F ABCDEFGHIJKLM 1 BRIGHT
		BF4C ABCDABCDABCDABCDABCDABCD 1 BRIGHT
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		TNT1 A 0 A_PlaySound("D4BFG/Fire2",1)
		TNT1 A 0 A_FireCustomMissile("64BFGBallHuge",0,1,6,-3,0,0)
		TNT1 A 0 A_TakeInventory("Cell",100)
		BF4F M 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		BF4F NOPQ 1 BRIGHT A_SetAngle(random(1,-1) + angle)
		BF4G A 6
		TNT1 A 0 A_Refire
		BF4G A 6 A_WeaponReady(1)
		Goto Ready
	NoUpgrades:
		TNT1 A 0 A_Print("You have no upgrades for this weapon",2)
		Goto Ready
	NoUpgrades2:
		TNT1 A 0 A_Print("You don't have a level 2 upgrade for this weapon",2)
		Goto Ready
	NoAmmo:
		TNT1 A 0 A_Print("You don't have enough ammo to use this weapon's alt fire",2)
		Goto Ready
	UpgradeWeapons:
		TNT1 A 0 A_TakeInventory("IsUpgradingWeapons",1)
		TNT1 A 0 SetPlayerProperty(0,1,4)
		PK4P ABCDEFGH 2
		TNT1 A 6
		TNT1 A 0 A_PlaySound("UACBot/Pickup")
		TNT1 A 0 SetPlayerProperty(0,0,4)
		TNT1 A 12
		Goto Select
	}
}

Actor 64BFGBallDeathSound : 64BFGBall
{
DeathSound "D4BFG/BallImpact"
}

ACTOR PurgeBolt : BaronBall
{
Scale 1.6
Damage 28
Speed 30
Radius 10
Height 16
Alpha 0.9
RenderStyle Add
SeeSound ""
DeathSound "D4BFG/MiniImpact"
Decal "Scorch"
DamageType "BFG"	// WAS desintegrate
+RANDOMIZE
+FORCEXYBILLBOARD
+THRUGHOST
+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("GreenFlare22")
		TNT1 A 0 A_RadiusThrust(-100,100)
		FRPG K 1 Bright Light("BFGBALL_X5")
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 Radius_Quake(2,54,0,15,0)
		TNT1 A 0 A_RadiusThrust(-900,600)
		BFE1 A 1 Bright Light("BFGBALL_X5") A_Explode(30,100,1)
		TNT1 A 0 A_RadiusThrust(-900,600)
		BFE1 B 1 Bright Light("BFGBALL_X5") A_Explode(30,100,1)
		TNT1 A 0 A_RadiusThrust(-900,600)
		BFE1 C 1 Bright Light("BFGBALL_X5") A_Explode(30,100,1)
		TNT1 A 0 A_RadiusThrust(-900,600)
		BFE1 D 1 Bright Light("BFGBALL_X5") A_Explode(30,100,1)
		TNT1 A 0 A_RadiusThrust(-900,600)
		BFE1 EF 1 Bright Light("BFGBALL_X5") A_Explode(30,100,1)
		Stop
	}
}

Actor 64BFGBallHuge : BFGBall
{
Damage 250
Speed 5
Scale 0.75
Radius 8
Height 8
Renderstyle Normal
DamageType "BFG"
Decal "BFGLIGHTNING"
DeathSound "D4BFG/BallImpact2"
+EXTREMEDEATH
+FRIENDLY
	states
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("64BFGBallHugeTracerSpawner")
		BFS1 A 1 Bright A_SpawnItemEx("BFGFlareHuge")
		TNT1 A 0 A_SpawnItemEx("64BFGBallHugeTracerSpawner")
		BFS1 A 1 Bright A_SpawnItemEx("BFGFlareHuge")
		TNT1 A 0 A_SpawnItemEx("64BFGBallHugeTracerSpawner")
		BFS1 B 1 Bright A_SpawnItemEx("BFGFlareHuge")
		TNT1 A 0 A_SpawnItemEx("64BFGBallHugeTracerSpawner")
		BFS1 B 1 Bright A_SpawnItemEx("BFGFlareHuge")
		Loop
	Death:
		TNT1 AAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavy",0,0,random(0,360),2,random(10,70))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleGreen",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAAAA 0 A_CustomMissile("64BFGFireBaseHuge",0,0,random(0,360),2,random(0,360))
		TNT1 AAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 Radius_Quake(4,8,0,12,0)
		TNT1 A 0 A_Explode(200,180,0)
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		BFE1 A 8 BRIGHT
		BFE1 B 7 BRIGHT A_FadeOut(0.14)
		BFE1 C 3 BRIGHT A_FadeOut(0.14)
		TNT1 A 0 A_BFGSpray("64BFGExtraHuge",50)
		BFE1 D 3 BRIGHT A_FadeOut(0.14)
		BFE1 E 3 BRIGHT A_FadeOut(0.14)
		BFE1 F 3 BRIGHT A_FadeOut(0.14)
		Stop
	}
}

ACTOR 64BFGBallHugeTracerSpawner     
{
    Radius 6
    Height 8
    Speed 1
    Damage 0
    -BLOODSPLATTER 
    +NOBOSSRIP
    +NOBLOOD 
    +PAINLESS 
    +RIPPER
    +DOOMBOUNCE
    +NOTIMEFREEZE
	+FRIENDLY
    BloodType AXEDT
    Scale 1
    DAMAGETYPE "BFGD"
    PROJECTILE
    RENDERSTYLE ADD
    +CLIENTSIDEONLY
    ALPHA 0.6
    States
    {
    Spawn:
       TNT1 A 0 Bright
       TNT1 A 1 Bright A_SpawnItemEx("64BFGBallHugeTracer",0,0,0,3,0,0,  0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
       TNT1 A 1 Bright A_SpawnItemEx("64BFGBallHugeTracer",0,0,0,3,0,0,  60,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
       TNT1 A 1 Bright A_SpawnItemEx("64BFGBallHugeTracer",0,0,0,3,0,0,  120,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS) 
	   Stop
    }
}

ACTOR 64BFGBallHugeTracer
{  
    MONSTER
    Radius 1
    Height 1
    Speed 0
    +PAINLESS
    -BLOODSPLATTER 
    +NOBOSSRIP
    +NOBLOOD 
    +NOTIMEFREEZE
    +NOGRAVITY
    +NOCLIP
    +LOOKALLAROUND
    +MISSILEMORE
    +MISSILEEVENMORE
	+FRIENDLY
    -SOLID
    -SHOOTABLE
	DAMAGETYPE "BFGD"
    MinMissileChance 0
    RenderStyle None
    ReactionTime 25
    States
    {
    Spawn:
	   NULL A 0 A_CountDown
       XXXX A 1 A_Look
//    loop
    See:
       XXXX A 0 A_Chase
    Missile:
       XXXX A 0 A_FaceTarget
       XXXX A 0 A_CustomRailgun(random(15,30),0,none,none,RGF_FULLBRIGHT|RGF_SILENT|RGF_NOPIERCING,10,0,"64BFGBallHugeTracerPuff",0,0,0,0,30,0,"64BFGBallHugeTracerTrail")
   Stop
    }
}

actor 64BFGBallHugeTracerTrail
{
  game Doom
  height 2
  radius 2
  speed 0
  damage 0
  Renderstyle Add
  Alpha 1
  seesound "null"
  deathsound "null"
  DamageType "BFGD"
  PROJECTILE
  +RANDOMIZE
  +FORCEXYBILLBOARD
  +FRIENDLY
  states
  {
  Spawn:
    BF4T ABC 1 Bright A_FadeOut (0.333)
    loop
  }
}
ACTOR 64BFGBallHugeTracerPuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+NOTELEPORT
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	Renderstyle Add
	Scale 0.25
	DamageType "BFGD"
	States
	{
	Spawn:
		TNT1 A 0 Bright
		Stop
	}
}

Actor 64BFGFireBaseHuge
{
Scale 1.2
Speed 6
Radius 1
Height 1
Alpha 0.8
RenderStyle Add
BounceType "Doom"
BounceFactor 0.5
+NOBLOCKMAP
+NOTELEPORT
+DONTSPLASH
+MISSILE
+FORCEXYBILLBOARD
+CLIENTSIDEONLY
+NOINTERACTION
+NOGRAVITY
+THRUACTORS
	States
	{
	Spawn:
		BFE1 ABCDEF 4 Bright
		Stop
	}
}

Actor 64BFGExtraHuge : BFGExtra
{
Renderstyle Normal
DamageType "BFG"
+EXTREMEDEATH
	states
	{
	Spawn:
		BFE2 ABCDEF 2 Bright
		Stop
	}
}

Actor BFGFlareHuge : LensFlareAlt
{
Scale 2
Alpha 0.5
-FORCEYBILLBOARD
+FORCEXYBILLBOARD
+RANDOMIZE
	states
	{
	Spawn:
		BFS1 A 2 NoDelay Bright A_SpawnItemEX("LensFlareTorchGreen")
		TNT1 A 0 A_CustomMissile("ExplosionParticleGreenHeavySmall",0,0,random(0,360),2,random(20,50))
		TNT1 A 0 A_CustomMissile("ExplosionParticleGreenVeryFast",0,0,random(0,360),2,random(20,40))
		BFS1 B 2 Bright A_SpawnItemEX("LensFlareTorchGreen")
		BFE2 AABBCC 2 Bright A_SpawnItemEX("GreenLensFlareAltSmallZZ")
		BFE2 DEF 2 Bright
		Stop
	}
}
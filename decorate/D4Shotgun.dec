ACTOR D4Shotgun : BrutalD464Weapon Replaces ShotgunSpawner
{
Weapon.SelectionOrder 1300
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Weapon.AmmoType "Shell"
Weapon.BobStyle InverseSmooth
Weapon.BobRangeX 0.4
Weapon.BobRangeY 0.3
Weapon.BobSpeed 1.8
Inventory.PickupSound "Shotgun/Pickup"
Inventory.PickupMessage "You got the Combat Shotgun!"
Obituary "$OB_MPSHOTGUN"
Tag "$TAG_SHOTGUN"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOFIRE
+inventory.IGNORESKILL
	States
	{
	Spawn:
		D4W3 A -1
		Stop
	KickingFlash:
		SHTS CBA 1
		SHTS A 10
		SHTS ABC 1
		Stop
	AirKickingFlash:
		SHTS CBA 1
		SHTS A 12
		SHTS ABC 1
		Stop
	SlideKickingStart:
		SHTS CBA 1
		SHTS A 19
	SlideKickingEnd:
		SHTS A 7
		SHTS ABC 1
		Stop
	v1ready:
	Ready:
		SHTG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"ChangeAltMode")
		Loop
	ChangeAltMode:
		SHTG A 1
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 A_JumpIfInventory("ShotgunUpgrade",1,1)
		Goto NoUpgrades
		SHTG A 1
		TNT1 A 0 A_JumpIfInventory("ShotgunUpgrade",2,1)
		Goto NoUpgrades2
		SHTG A 1
		TNT1 A 0 A_JumpIfInventory("D4AltMode",1,"ChangeAltMode2")
		TNT1 A 0 A_GiveInventory("D4AltMode",1)
		TNT1 A 0 A_Print("Explosive Shells Selected",2)
		SHTS CBA 1
		SHTS A 6
		SHTS ABC 1
		SHTF EFGH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		Goto Ready
	ChangeAltMode2:
		SHTG A 1
		TNT1 A 0 A_TakeInventory("D4AltMode",1)
		TNT1 A 0 A_Print("Burst Fire Mode Selected",2)
		SHTS CBA 1
		SHTS A 6
		SHTS ABC 1
		SHTF EFGH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		Goto Ready
	Deselect:
		SHTS CBA 1
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4ShotgunSelected",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4ShotgunSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		SHTS ABC 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 AAAAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,6,-6,0,random(-3,3))
		TNT1 A 0 A_FireBullets(4.2,3.9,7,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("Shotgun/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SHTF A 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		SHTF B 1
		SHTF CCD 1 A_SetAngle(random(1,-1) + angle)
		SHTG A 6 A_SetAngle(random(1,-1) + angle)
		SHTF EFGH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-6)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		SHTG A 2
		TNT1 A 0 A_Refire
		SHTG A 5 A_WeaponReady
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("ShotgunUpgrade",1,1)
		Goto NoUpgrades
		TNT1 A 0 A_JumpIfInventory("D4AltMode",1,"AltFire2")
		TNT1 A 0 A_JumpIfInventory("Shell",3,1)
		Goto NoAmmo
		TNT1 AAAAAAA 0 A_FireCustomMissile("Tracer",random(-3,3),0,6,-6,0,random(-3,3))
		TNT1 A 0 A_FireBullets(4.2,3.9,7,7,"HitPuff")
		TNT1 A 0 A_PlaySound("Shotgun/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SHTF A 1 BRIGHT A_SetAngle(random(5,-5) + angle)
		SHTF B 1 A_TakeInventory("Shell",1)
		SHTF CCD 1 A_SetAngle(random(1,-1) + angle)
		SHTG A 1
		TNT1 AAAAAAA 0 A_FireCustomMissile("Tracer",random(-5,5),0,12,-12,0,random(-1,1))
		TNT1 A 0 A_FireBullets(4.2,3.9,7,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("Shotgun/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SHTF A 1 BRIGHT A_SetAngle(random(5,-5) + angle)
		SHTF B 1 A_TakeInventory("Shell",1)
		SHTF CCD 1 A_SetAngle(random(1,-1) + angle)
		SHTG A 1
		TNT1 AAAAAAA 0 A_FireCustomMissile("Tracer",random(-5,5),0,12,-12,0,random(-1,1))
		TNT1 A 0 A_FireBullets(4.2,3.9,7,7,"ShotgunPuff")
		TNT1 A 0 A_PlaySound("Shotgun/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SHTF A 1 BRIGHT A_SetAngle(random(5,-5) + angle)
		SHTF B 1 A_TakeInventory("Shell",1)
		SHTF CCD 1 A_SetAngle(random(1,-1) + angle)
		SHTG A 1 A_SetAngle(random(1,-1) + angle)
		SHTF EFGH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-6)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		SHTF GH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-6)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		SHTF GH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-6)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		SHTG A 1
		TNT1 A 0 A_Refire
		SHTG A 5 A_WeaponReady
		Goto Ready
	AltFire2:
		TNT1 A 0 A_JumpIfInventory("Shell",4,1)
		Goto NoAmmo
		TNT1 A 0 A_TakeInventory("Shell",4)
		TNT1 A 0 A_PlaySound("Shotgun/GrenadeFire",1)
		TNT1 A 0 A_FireCustomMissile("ShotgunGrenade",random(-1,1),0,6,-2,0,random(-1,1))
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		SHTF N 2 BRIGHT A_SetAngle(random(4,-4) + angle)
		SHTF B 1
		SHTF CCD 1 A_SetAngle(random(1,-1) + angle)
		SHTG A 6 A_SetAngle(random(1,-1) + angle)
		SHTF EFGH 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpIn",7)
		TNT1 A 0 A_FireCustomMissile("ShotCaseSpawn",0,0,17,-6)
		SHTF IJKLMMLKJ 1
		TNT1 A 0 A_PlaySound("Shotgun/PumpOut",7)
		SHTF IHGFE 1
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		SHTG A 2
		TNT1 A 0 A_Refire
		SHTG A 5 A_WeaponReady
		Goto Ready
	NoUpgrades:
		TNT1 A 0 A_Print("You have no upgrades for this weapon",2)
		Goto Ready
	NoUpgrades2:
		TNT1 A 0 A_Print("You don't have a level 2 upgrade for this weapon",2)
		Goto Ready
	NoAmmo:
		TNT1 A 0 A_Print("You don't have enough ammo to use this weapon's alt fire",2)
		Goto Ready
	UpgradeWeapons:
		TNT1 A 0 A_TakeInventory("IsUpgradingWeapons",1)
		TNT1 A 0 SetPlayerProperty(0,1,4)
		PK4P ABCDEFGH 2
		TNT1 A 6
		TNT1 A 0 A_PlaySound("UACBot/Pickup")
		TNT1 A 0 SetPlayerProperty(0,0,4)
		TNT1 A 12
		Goto Select
	}
}

ACTOR ShotgunGrenade : 64RevenantTracer3
{
Scale 0.5
Damage 10
Speed 40
Radius 4
Height 4
Gravity 0.4
Renderstyle Add
-thruactors
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("CoolAndNewFlameTrails",0,0,random(-2,2))
		FR7X A 1 BRIGHT A_SpawnItemEx("RedFlareSmall")
		TNT1 A 0 A_CustomMissile("OldschoolRocketSmokeTrail2",2,0,random(160,210),2,random(-30,30))
		TNT1 A 0 A_SpawnItem("CoolAndNewFlameTrails")
		FR7X B 1 BRIGHT A_SpawnItemEx("RedFlareSmall")
		Loop
	Death:
		TNT1 A 0 Radius_Quake(3,8,0,15,0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItemEx("ExplosionFlareSpawner")
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("MediumExplosionFlames",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("RocketExplosion2",0,0,13)
		TNT1 A 2
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 A 20
		Stop
	}
}

actor RocketExplosion2
{
+MISSILE
Radius 2
Height 2
Damagetype ExplosiveImpact
	States
	{
	Spawn:
		Goto Death
	Death:
		TNT1 A 3
		TNT1 A 1 A_Explode(70,85)
	Stop
	}
}
ACTOR D4GaussCannon : BrutalD464Weapon Replaces GaussCannonSpawner
{
Weapon.SelectionOrder 2799
Weapon.AmmoUse 15
Weapon.AmmoGive 30
Weapon.AmmoType "Cell"
Weapon.BobStyle InverseSmooth
Weapon.BobRangeX 0.5
Weapon.BobRangeY 0.3
Weapon.BobSpeed 1.7
Inventory.PickupMessage "You got the Gauss Cannon!"
Inventory.PickupSound "D4GaussCannon/Pickup"
Obituary "$OB_RAILGUN"
Tag "Gauss Cannon"
+INVENTORY.RESTRICTABSOLUTELY
+inventory.ignoreskill
+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		D4W7 B -1
		Stop
	KickingFlash:
		GCAS CBA 1
		GCAS A 10
		GCAS ABC 1
		Stop
	AirKickingFlash:
		GCAS CBA 1
		GCAS A 12
		GCAS ABC 1
		Stop
	SlideKickingStart:
		GCAS CBA 1
		GCAS A 19
	SlideKickingEnd:
		GCAS A 7
		GCAS ABC 1
		Stop
	v1Ready:
	Ready:
		GCAG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"ChangeAltMode")
		Loop
	ChangeAltMode:
		GCAG A 1
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 A_JumpIfInventory("GaussUpgrade",1,1)
		Goto NoUpgrades
		GCAG A 1
		TNT1 A 0 A_JumpIfInventory("GaussUpgrade",2,1)
		Goto NoUpgrades2
		TNT1 A 0 A_JumpIfInventory("D4AltMode",1,"ChangeAltMode2")
		TNT1 A 0 A_GiveInventory("D4AltMode",1)
		TNT1 A 0 A_Print("Railgun Mode Selected",2)
		TNT1 A 0 A_PlaySound("D4GaussCannon/Electricity",6)
		GCAF AB 2 Bright
		TNT1 A 0 A_PlaySound("D4GaussCannon/ModSwitch",CHAN_WEAPON)
		GCAF CB 2 Bright
		TNT1 A 0 A_StopSound(6)
		Goto Ready
	ChangeAltMode2:
		GCAG A 1
		TNT1 A 0 A_TakeInventory("D4AltMode",1)
		TNT1 A 0 A_Print("Gauss Bomb mode Selected",2)
		TNT1 A 0 A_PlaySound("D4GaussCannon/Electricity",6)
		GCAF AB 2 Bright
		TNT1 A 0 A_PlaySound("D4GaussCannon/ModSwitch",CHAN_WEAPON)
		GCAF CB 2 Bright
		TNT1 A 0 A_StopSound(6)
		Goto Ready
	Deselect:
		GCAS CBA 1
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("D4GaussSelected",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4GaussSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		GCAS ABC 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	Fire:
		TNT1 A 0 A_PlaySound("D4GaussCannon/Electricity",6)
		GCAF ABCD 1 BRIGHT
		TNT1 A 0 A_PlaySound("D4GaussCannon/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GaussCannonShot",0,1,6,-3,0,0)
		GCAF E 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		GCAF F 1 BRIGHT
		TNT1 A 0 A_StopSound(6)
		GCAF GHIJ 1 A_SetAngle(random(1,-1) + angle)
		GCAG A 15
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("GaussUpgrade",1,1)
		Goto NoUpgrades
		TNT1 A 0 A_JumpIfInventory("D4AltMode",1,"AltFire2")
		TNT1 A 0 A_JumpIfInventory("cell",15,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("D4GaussCannon/Charge",7)
		TNT1 A 0 A_PlaySound("D4GaussCannon/Electricity",6)
		GCAF ABABAB 1 BRIGHT
		GCAF CDCDCD 1 BRIGHT
		TNT1 A 0 A_PlaySound("D4GaussCannon/AltFire",1)
		TNT1 A 0 A_TakeInventory("cell",15)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GaussBomb",0,1,6,-3,0,0)
		GCAF E 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		GCAF F 1 BRIGHT
		TNT1 A 0 A_StopSound(6)
		GCAF GGGG 1 A_SetAngle(random(1,-1) + angle)
		GCAF GHIJ 1
		GCAG A 5
		Goto Ready
	AltFire2:
		TNT1 A 0 A_JumpIfInventory("cell",10,1)
		Goto NoAmmo
		TNT1 A 0 A_PlaySound("D4GaussCannon/Electricity",6)
		GCAF ABCD 1 BRIGHT
		TNT1 A 0 A_PlaySound("D4GaussCannon/RailFire",CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("cell",10)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle3",20,5,22)
		TNT1 A 0 A_RailAttack(160,8,0,None,None,RGF_FULLBRIGHT|RGF_SILENT,0,"GaussCannonProjectilePuff",0,0,0,35,5,0,"GaussCannonProjectileTrail",-5)
		GCAF E 1 BRIGHT A_SetAngle(random(4,-4) + angle)
		GCAF F 1 BRIGHT
		TNT1 A 0 A_StopSound(6)
		GCAF GHIJ 1 A_SetAngle(random(1,-1) + angle)
		GCAG A 15
		Goto Ready
	NoUpgrades:
		TNT1 A 0 A_Print("You have no upgrades for this weapon",2)
		Goto Ready
	NoUpgrades2:
		TNT1 A 0 A_Print("You don't have a level 2 upgrade for this weapon",2)
		Goto Ready
	NoAmmo:
		TNT1 A 0 A_Print("You don't have enough ammo to use this weapon's alt fire",2)
		Goto Ready
	UpgradeWeapons:
		TNT1 A 0 A_TakeInventory("IsUpgradingWeapons",1)
		TNT1 A 0 SetPlayerProperty(0,1,4)
		PK4P ABCDEFGH 2
		TNT1 A 6
		TNT1 A 0 A_PlaySound("UACBot/Pickup")
		TNT1 A 0 SetPlayerProperty(0,0,4)
		TNT1 A 12
		Goto Select
	}
}

Actor GaussCannonShot : FastProjectile
{
scale 0.9
damage(random(40,50))
speed 190
height 8
radius 8
Alpha 0.9
RenderStyle Add
damagetype "Plasma"
Decal "Scorch"
+BLOODSPLATTER
+RIPPER
+FORCERADIUSDMG
+EXTREMEDEATH
-DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("gaussheavyplasmaTracerTrail",0,0,2)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail2",(2 *momx)/-35.0,-(2 *momy)/-35.0,2+(2 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail3",(4 *momx)/-35.0,-(4 *momy)/-35.0,2+(4 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail4",(6 *momx)/-35.0,-(6 *momy)/-35.0,2+(6 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail5",(8 *momx)/-35.0,-(8 *momy)/-35.0,2+(8 *momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail6",(10*momx)/-35.0,-(10*momy)/-35.0,2+(10*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail7",(12*momx)/-35.0,-(12*momy)/-35.0,2+(12*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail8",(14*momx)/-35.0,-(14*momy)/-35.0,2+(14*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail9",(16*momx)/-35.0,-(16*momy)/-35.0,2+(16*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail10",(18*momx)/-35.0,-(18*momy)/-35.0,2+(18*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail11",(20*momx)/-35.0,-(20*momy)/-35.0,2+(20*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("gaussheavyplasmaTracerTrail12",(22*momx)/-35.0,-(22*momy)/-35.0,2+(22*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_CustomMissile("PlasmaSmoke",1,0,random(0,360),2,random(0,160))
//		TNT1 A 0 A_CustomMissile("BluePlasmaFireNonStatic",2,0,random(0,360),2,random(0,180))
		TNT1 AAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360))
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItem("BlueFlare")
//		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaFireNonStatic",2,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
//		TNT1 A 0 A_SpawnItemEx("UnderwaterExplosion")
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 A 0 A_SpawnItemEx("RocketExplosion",0,0,13)
		TNT1 AAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		BL1I ABCD 1 BRIGHT A_SpawnItem("BlueFlare")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 1 A_Explode(200,100)
		Stop
	}
}

Actor GaussBomb : FastProjectile
{
scale 0.9
damage 50
speed 90
height 8
radius 8
gravity 1
Alpha 0.9
RenderStyle Add
damagetype Explosive	//plasma
Decal "Scorch"
+BLOODSPLATTER
+FORCERADIUSDMG
-DONTSPLASH
-nogravity
+EXTREMEDEATH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("gaussheavyplasmaTracerTrail",0,0,2)
		TNT1 AAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360))
		Loop
	Death:
		TNT1 AAAAA 0 A_SpawnItemEx("BlueFlare")
//		TNT1 AAAAA 0 A_CustomMissile("BluePlasmaFireNonStatic",2,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("BlueplasmaParticle",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_PlaySound("FAREXPL",3)
		TNT1 AAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater")
		BL1I ABCD 1 BRIGHT A_SpawnItem("BlueFlare")
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		TNT1 A 1 A_Explode(300,180)
		Stop
	}
}

actor gaussheavyplasmaTracerTrail
{
Scale 0.4
Alpha 0.9
RenderStyle Add
+NOINTERACTION
+CLIENTSIDEONLY
	States
	{
	Spawn:
		SPKB A 2 BRIGHT
		stop
	}
}

actor gaussheavyplasmaTracerTrail2 : gaussheavyplasmaTracerTrail	{Alpha 0.85}
actor gaussheavyplasmaTracerTrail3 : gaussheavyplasmaTracerTrail	{Alpha 0.80}
actor gaussheavyplasmaTracerTrail4 : gaussheavyplasmaTracerTrail	{Alpha 0.75}
actor gaussheavyplasmaTracerTrail5 : gaussheavyplasmaTracerTrail	{Alpha 0.70}
actor gaussheavyplasmaTracerTrail6 : gaussheavyplasmaTracerTrail	{Alpha 0.65}
actor gaussheavyplasmaTracerTrail7 : gaussheavyplasmaTracerTrail	{Alpha 0.60}
actor gaussheavyplasmaTracerTrail8 : gaussheavyplasmaTracerTrail	{Alpha 0.55}
actor gaussheavyplasmaTracerTrail9 : gaussheavyplasmaTracerTrail	{Alpha 0.50}
actor gaussheavyplasmaTracerTrail10 : gaussheavyplasmaTracerTrail	{Alpha 0.45}
actor gaussheavyplasmaTracerTrail11 : gaussheavyplasmaTracerTrail	{Alpha 0.40}
actor gaussheavyplasmaTracerTrail12 : gaussheavyplasmaTracerTrail	{Alpha 0.35}
actor gaussheavyplasmaTracerTrail13 : gaussheavyplasmaTracerTrail	{Alpha 0.30}
actor gaussheavyplasmaTracerTrail14 : gaussheavyplasmaTracerTrail	{Alpha 0.25}
actor gaussheavyplasmaTracerTrail15 : gaussheavyplasmaTracerTrail	{Alpha 0.20}
actor gaussheavyplasmaTracerTrail16 : gaussheavyplasmaTracerTrail	{Alpha 0.15}
actor gaussheavyplasmaTracerTrail17 : gaussheavyplasmaTracerTrail	{Alpha 0.10}
actor gaussheavyplasmaTracerTrail18 : gaussheavyplasmaTracerTrail	{Alpha 0.05}

ACTOR GaussRailPuff : HitPuff
{
DamageType Kick
Decal "SmallerScorch"
-NOEXTREMEDEATH
+EXTREMEDEATH
+ALWAYSPUFF
+FORCEDECAL
	states
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItemEx("GaussRicoChet",0,0,-5)
		//Intentional fall-through
	Melee:
		TNT1 A 1
		stop
	}
}

ACTOR GaussRicoChet : RicoChet
{
	states
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall")
		TNT1 A 0 A_SpawnItemEx("UnderwaterHitPuff")
		TNT1 A 1
		Stop
	}
}
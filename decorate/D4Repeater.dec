ACTOR D4Repeater : BrutalD464Weapon replaces SMGSpawner
{
Scale 0.7
Weapon.SelectionOrder 800
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Weapon.AmmoType "Clip"
Inventory.PickupSound "Repeater/Pickup"
Inventory.PickupMessage "You got the Repeater Rifle!"
Obituary "%o was picked off by %k's rifle."
Tag "Repeater Rifle"
+INVENTORY.RESTRICTABSOLUTELY
+inventory.IGNORESKILL
+WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		D4W2 B -1
		Stop
	KickingFlash:
		RPTG A 18 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	SlideKickingStart:
		RPTG A 20 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
	SlideKickingEnd:
		RPTG A 8 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		Stop
	v1ready:
	Ready:
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"NoAltMode")
		RPTG A 1 A_WeaponReady
		Loop
	Ready2:
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("WeaponAltMode",1,"NoAltMode")
		RPTG E 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("IsD4Weapon",0)
		TNT1 A 0 A_TakeInventory("Zoomed",0)
		TNT1 A 0 A_TakeInventory("D4PeaGunSelected",0)
		TNT1 A 0 A_TakeInventory("D4AltMode",0)
		RPTG A 1 Offset(-8, 37)
		RPTG A 1 Offset(-20, 43)
		RPTG A 1 Offset(-45, 55)
		RPTG A 1 Offset(-70, 68)
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("D4PeaGunSelected",1)
		TNT1 A 0 A_GiveInventory("IsD4Weapon",1)
		RPTG A 1 Offset(75, 75)
		RPTG A 1 Offset(50, 60)
		RPTG A 1 Offset(28, 45)
		RPTG A 1 Offset(10, 34)
		TNT1 A 0 Offset(0,32) A_Raise
		Wait
	Fire:
		TNT1 A 0 A_FireCustomMissile("Tracer",random(-1,1),1,12,-6,0,random(-1,1))
		TNT1 A 0 A_Firecustommissile("50CaseSpawn",0,1,10,-12)
		TNT1 A 0 A_FireBullets(1.0,1.0,1,7,"MachinegunBulletPuff")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomedFire")
		TNT1 A 0 A_PlaySound("Repeater/Fire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		RPTF A 1 BRIGHT A_SetAngle(random(1,-1) + angle)
		RPTF B 1 BRIGHT A_GiveInventory("BurstFiredSMG",1)
		TNT1 A 0 A_JumpIfInventory("BurstFiredSMG",3,1)
		Goto Fire
		TNT1 A 0 A_TakeInventory("BurstFiredSMG",0)	
		RPTF CD 1 A_SetAngle(random(1,-1) + angle)
		RPTG A 1 A_SetAngle(random(1,-1) + angle)
		RPTG A 6 A_WeaponReady(1)
		TNT1 A 0 A_Refire
		Goto Ready
	ZoomedFire:
		TNT1 A 0 A_PlaySound("Repeater/MagnumFire",1)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		RPTG F 1 BRIGHT A_SetAngle(random(1,-1) + angle)
		RPTG GHI 1
		RPTG E 3
		RPTG E 8 A_WeaponReady(1)
		TNT1 A 0 A_Refire
		Goto Ready2
	AltFire:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"StopAlt")
		TNT1 A 0 A_ZoomFactor(1.25)
		TNT1 A 0 A_GiveInventory("Zoomed",1)
		RPTG BCD 1
		Goto Ready2
	StopAlt:
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 A_TakeInventory("Zoomed",1)
		RPTG DCB 1
		Goto Ready
	NoAltMode:
		TNT1 A 0 A_TakeInventory("WeaponAltMode",1)
		TNT1 A 0 ACS_NamedExecuteWithResult("NoUpgradedWeapons",3)
		Goto Ready		
	}
}

Actor PlasMag : FastProjectile
{
scale 0.2
damage 70
speed 200
Alpha 0.9
radius 2
height 2
RenderStyle Add
Decal "LaserDecal"
+BLOODSPLATTER
-DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 1 NoDelay A_SpawnItemEx("PistolplasmaTracerTrail",0,0,2)
		TNT1 A 0 A_SpawnItemEx("PistolplasmaTracerTrail2",(2*momx)/-35.0,-(2*momy)/-35.0,2+(2*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("PistolplasmaTracerTrail3",(4*momx)/-35.0,-(4*momy)/-35.0,2+(4*momz)/-35.0,0,0,0,0,SXF_ABSOLUTEANGLE)
		TNT1 A 0 A_SpawnItemEx("redFlareSmall")
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("DetectFloorLaser")
		TNT1 A 0 A_SpawnItemEx("DetectCeilLaser")
		TNT1 AAAAA 0 A_SpawnItemEx("redFlareSmall")
		BL1I ABCD 1 BRIGHT A_SpawnItemEx("redFlareSmall")
		Stop
	}
}